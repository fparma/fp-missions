leaFunction =  compile preprocessFileLineNumbers "lea\loadout-init.sqf"; call leaFunction;//line generated by LEA.
enableSaving [false, false];


//turn off conversations; script so AI can hear us talk on ACRE if close enough and finally remove player corpses script
if (!isDedicated) then {
	waitUntil {player == player};
	["ACRE_PRC148", [5000, 5000, 5000, 5000] ] call acre_api_fnc_setDefaultPowers;
	 ["ACRE_PRC117F", [20000, 20000, 20000, 20000] ] call acre_api_fnc_setDefaultPowers;
	0 fadeRadio 0;
	player setVariable ["BIS_noCoreConversations", true];
	[] execVM "scripts\aiHearTalking.sqf"; 
	player addEventHandler ["Respawn",{_this execVM "scripts\burial.sqf"}];
	
	BB_teleport = {
		waitUntil {alive  player};
		cutText ["","BLACK FADED",1];
		_tp = MED1;
		if (!alive _tp) exitWith {};
		if (_tp emptyPositions "cargo" > 0 ) then {
			player assignAsCargo _tp;
			sleep 1;
			player moveInCargo _tp;
		}else{
			if (_tp emptyPositions "driver" > 0) then {
				player assignAsDriver _tp;
				sleep 1;
				player moveInDriver _tp;
			}else{
				_pos = _tp findEmptyPosition  [5,100, typeOf player];
				player setPos _pos;
			};
		};
		cutText ["","BLACK IN",1];
		"dynamicBlur" ppEffectEnable true;   
		"dynamicBlur" ppEffectAdjust [6];   
		"dynamicBlur" ppEffectCommit 0;     
		"dynamicBlur" ppEffectAdjust [0.0];  
		"dynamicBlur" ppEffectCommit 5;
	};
	
	["ace_sys_wounds_rev2", {
		(_this select 0) spawn BB_teleport;
	}] call CBA_fnc_addEventhandler;
};

// *********** ACE WOUNDS ************
//How much damage is max left when healed in the field, (is multiplied times the amount the unit was healed in the field already), instead of healed at a medical facility
ace_sys_wounds_leftdam  = 0.2;
//Timer before respawn
ace_wounds_prevtime = 400;
//Should player start with medical gear?
ace_sys_wounds_no_medical_gear = false;
//remove comment if you want the player to be able to respawn without having to wait for the timer.
//ace_sys_wounds_no_rpunish = true;
// more info: http://wiki.ace-mod.net/Wounding_System
// *********** ACE WOUNDS ************

//dont put gear in a bag when pilot or crew member
if (isServer) then {
	ace_sys_eject_fnc_weaponCheckEnabled = {false}; 
	publicVariable "ace_sys_eject_fnc_weaponCheckEnabled";
};

//view distance and grass
setViewDistance 2500;
setTerrainGrid 25;

[0,1,1] execVM "scripts\DRN\DynamicWeatherEffects\DynamicWeatherEffects.sqf";

processInitCommands;
finishMissionInit;

//lower weapon at player start
player switchMove "amovpercmstpslowwrfldnon_player_idlesteady03";  